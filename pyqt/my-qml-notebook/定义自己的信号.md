当自定义类型不可避免，当你需要通知别的对象你的状态发生了变化，当你对象的粉丝想了解你对象的近况……此时此刻，彼时彼刻，最好的方法， Qt 给我们的，QML 给我们的，还是信号。

现在我们就来看怎么定义自己的信号。
### 自定义信号与使用
如果你自己定义新的 QML 类型，可以使用 signal 关键字给你的类型添加信号。其语法如下：
```
signal <name>[([<type> <parameter name>[, ...]])]  
```
信号其实是个方法（函数），所以呢，它的发射，实际是通过调用以信号名为名的方法达成的。

举个实例的例子：我们在界面上放一个字符串，两个代表颜色的小方块，点小方块，字符串的颜色就变成小方块的颜色。先看代码（my_signal.qml）
```
import QtQuick 2.0  
import QtQuick.Controls 1.1  
  
Rectangle {  
    width: 320;  
    height: 240;  
    color: "#C0C0C0";  
      
    Text {  
        id: coloredText;  
        anchors.horizontalCenter: parent.horizontalCenter;  
        anchors.top: parent.top;  
        anchors.topMargin: 4;  
        text: "Hello World!";  
        font.pixelSize: 32;  
    }  
      
    Component {  
        id: colorComponent;  
        Rectangle {  
            id: colorPicker;  
            width: 50;  
            height: 30;  
            signal colorPicked(color clr);  
            MouseArea {  
                anchors.fill: parent  
                onPressed: colorPicker.colorPicked(colorPicker.color);  
            }  
        }  
    }  
      
    Loader{  
        id: redLoader;  
        anchors.left: parent.left;  
        anchors.leftMargin: 4;  
        anchors.bottom: parent.bottom;  
        anchors.bottomMargin: 4;  
        sourceComponent: colorComponent;  
        onLoaded:{  
            item.color = "red";  
        }  
    }  
      
    Loader{  
        id: blueLoader;  
        anchors.left: redLoader.right;  
        anchors.leftMargin: 4;  
        anchors.bottom: parent.bottom;  
        anchors.bottomMargin: 4;  
        sourceComponent: colorComponent;  
        onLoaded:{  
            item.color = "blue";  
        }  
    }  
      
    Connections {  
        target: redLoader.item;  
        onColorPicked:{  
            coloredText.color = clr;  
        }  
    }  
      
    Connections {  
        target: blueLoader.item;  
        onColorPicked:{  
            coloredText.color = clr;  
        }  
    }  
}  
```
 这次代码稍微有点长了。现在我来充当导游，请跟紧我哦，别担心，不会强制你们买东西的。
 
首先定义了一个 Text 对象，id 为 coloredText ，后面会根据这个 id 来改变它的颜色。

然后我定义了一个组件，组件内有一个 Rectangle 对象，这里是我们定义信号的地方。我设置 Rectangle 的尺寸，然后定义了信号 colorPicked ，语句如下：
>signal colorPicked(color clr); 

为了触发信号，我给 Rectangle 引入了 MouseArea 。MouseArea 是专门处理鼠标操作的 item ，这里我们先知道它有一个 onClicked() 信号就行了。我们给这个信号指定信号处理器，在信号处理器中调用我们刚定义的信号：colorPicker.colorPicked(colorPicker.color); ……如你所见，信号的触发就是一个函数调用。

组件是可以重复利用的，一个组件可以在一个单独的 qml 文件中定义，也可以嵌入到其它 qml 文档中来定义以方便简单组件的使用。这里我选择在主 qml 文档内嵌入组件的定义。定义好了组件，就可以使用 Loader 来加载组件了。

Loader 是专门用来动态创建组件的，它可以从 qml 文件中创建组件，也可以指定 sourceComponent 来创建，这里的示例，因为组件是嵌入在主 qml 文件中定义的，所以使用了 sourceComponent 方式。我给每个 Loader 一个 id ，以便后面连接时使用。我还使用 anchors 为 Loader 布局。最后呢，在 Loader 的 onLoaded 信号处理器内给 Rectangle 对象配置颜色。

创建完 Loader ，就是建立连接了。这里使用 Connections 来建立信号的连接，target 指向刚才说的 Loader 对象的 item 属性， item 属性实际指向 Loader 创建的对象。在 Connections 对象中，通过 onColorPicked 信号处理器响应用户点击操作。你看到了，我们定义 colorPicked 信号时命名的参数是 clr ，所以 Connections 的信号处理器中可以直接使用它给 coloredText 对象复制。

