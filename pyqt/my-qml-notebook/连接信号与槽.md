### 连接信号与槽

前面我们使用信号时，要么通过信号处理器，要么使用 Connections 对象。其实在 QML 中还有一种更一般的方式。先回想下 Qt C++ 中我们如何使用信号与槽…… QObject::connection() ，诺，木错，就是它了。对应的，在 QML 中，其实 signal 是个对象，它也有一个 connect() 方法，你可以使用它连接到任意的方法上哦。有 connect() 就有 disconnect() ，正确， signal 对象的的确确有这两个方法，允许我们使用它维护连接。

signal 对象的 connect() 方法允许你连接一个信号到另外一个信号或者方法。其实没差别，信号本身也是个方法（函数）。当信号发射时，连接到信号上的其它信号或方法就会被调用。

signal 对象的这种连接方式，使用起来比信号处理器更加灵活。前面介绍信号处理器时，已经提到，信号处理器和信号是一对一的关系。而 signal 对象的这种连接方式，使得一个信号能够连接多个方法。

举个简单的例子来看看如何使用吧。下面的代码（来自 Qt 帮助）， messageReceived 信号通过 connect() 方法连接到了三个方法上。

```
Rectangle {  
    id: relay;  
  
    signal messageReceived(string person, string notice);  
  
    Component.onCompleted: {  
        relay.messageReceived.connect(sendToPost);  
        relay.messageReceived.connect(sendToTelegraph);  
        relay.messageReceived.connect(sendToEmail);  
        relay.messageReceived("Tom", "Happy Birthday");  
    }  
  
    function sendToPost(person, notice) {  
        console.log("Sending to post: " + person + ", " + notice);  
    }  
    function sendToTelegraph(person, notice) {  
        console.log("Sending to telegraph: " + person + ", " + notice);  
    }  
    function sendToEmail(person, notice) {  
        console.log("Sending to email: " + person + ", " + notice);  
    }  
}  
```

上面是信号连接方法的示例，实际上信号也可以连接信号。我们说了，信号本质是一个方法（函数），所以，信号连接信号等同于信号连接方法。下面是一个简单的示例：
```
Rectangle {  
    id: forwarder;  
    width: 100;   
    height: 100;  
  
    signal send();  
    onSend: console.log("Send clicked");  
  
    MouseArea {  
        id: mousearea;  
        anchors.fill: parent;  
        onClicked: console.log("MouseArea clicked");  
    }  
  
    Component.onCompleted: {  
        mousearea.clicked.connect(send);  
    }  
}  
```
